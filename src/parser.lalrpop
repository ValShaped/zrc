// DO NOT IMPORT DIRECTLY -- USE API EXPOSED BY front::parser INSTEAD
use crate::front::ast::{expr::*, stmt::*, ty::*};
use crate::front::lexer;
use crate::{box_arguments, into_expr_type};
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, lexer::Tok, lexer::LexicalError>>);

CommaSeparated<T>: Vec<T> = {
    <e:T> <mut v:("," <T>)*> ","? => {
        let mut new = vec![e];
        new.append(&mut v);
        new
    }
};

CommaSeparatedWithoutTrailing<T>: Vec<T> = {
    <e:T> <mut v:("," <T>)*> => {
        let mut new = vec![e];
        new.append(&mut v);
        new
    }
}

pub Program: Vec<Declaration> = {
    <DeclarationList>,
};

DeclarationList: Vec<Declaration> = {
    (<Declaration>)*,
};

// See https://en.wikipedia.org/wiki/Dangling_else#Avoiding_the_conflict_in_LR_parsers for why this is necessary.
// The ClosedStmt rule REQUIRES that an 'else' clause is used, meaning that it will attach to the inner 'if' statement.
pub Stmt: Stmt = {
    <OpenStmt>,
    <ClosedStmt>
}

OpenStmt: Stmt = {
    "if" "(" <e:Expr> ")" <s:Stmt> => Stmt::IfStmt(e, Box::new(s)),
    "if" "(" <a:Expr> ")" <b:ClosedStmt> "else" <c:OpenStmt> => Stmt::IfElseStmt(a, Box::new(b), Box::new(c)),
    "while" "(" <a:Expr> ")" <b:OpenStmt> => Stmt::WhileStmt(a, Box::new(b)),
    "for" "(" <a:Declaration> ";" <b:Expr> ";" <c:Expr> ")" <d:OpenStmt> => Stmt::ForStmt { init: Box::new(a), cond: b, post: c, body: Box::new(d) },
};

ClosedStmt: Stmt = {
    <SimpleStmt>,
    "if" "(" <a:Expr> ")" <b:ClosedStmt> "else" <c:ClosedStmt> => Stmt::IfElseStmt(a, Box::new(b), Box::new(c)),
    "while" "(" <a:Expr> ")" <b:ClosedStmt> => Stmt::WhileStmt(a, Box::new(b)),
    "for" "(" <a:Declaration> ";" <b:Expr> ";" <c:Expr> ")" <d:ClosedStmt> => Stmt::ForStmt { init: Box::new(a), cond: b, post: c, body: Box::new(d) },
};

SimpleStmt: Stmt = {
    <Expr> ";" => Stmt::ExprStmt(<>),
    ";" => Stmt::EmptyStmt,
    "{" <l:StmtList?> "}" => Stmt::BlockStmt(l.unwrap_or(Vec::new())),
    <FlowStmt>,
    <Declaration> => Stmt::from(<>),
};

Declaration: Declaration = {
    <LetDeclaration>,
    // FIXME: This might allow code like for (fn foo() {};;) {} to work. Do we want this?
    <FunctionDeclaration>,
};

FunctionDeclaration: Declaration = {
    "fn" <i:IDENTIFIER> "(" <a:ArgumentDeclarationList?> ")" <r:("->" <Type>)?> "{" <s:StmtList?> "}" => Declaration::FunctionDefinition {
        name: IDENTIFIER::Identifier(i),
        parameters: a.unwrap_or(Vec::new()),
        return_type: r,
        body: s.unwrap_or(Vec::new())
    }
    // TODO: External declarations
};

ArgumentDeclarationList = CommaSeparated<ArgumentDeclaration>;

ArgumentDeclaration: ArgumentDeclaration = {
    <i:IDENTIFIER> <t:(":" <Type>)?> => ArgumentDeclaration {
        name: IDENTIFIER::Identifier(i),
        ty: t
    }
};

LetDeclaration: Declaration = {
    "let" <l:CommaSeparatedWithoutTrailing<
        (IDENTIFIER (":" <Type>)? ("=" <Assignment>)?)
    >> ";" => Declaration::DeclarationList(l.into_iter().map(|(i, t, v)| LetDeclaration {
        name: IDENTIFIER::Identifier(i),
        ty: t,
        value: v,
    }).collect::<Vec<_>>()),
};

Type: Type = {
    <IDENTIFIER> => Type::Identifier(IDENTIFIER::Identifier(<>)),
}

FlowStmt: Stmt = {
    "continue" ";" => Stmt::ContinueStmt,
    "break" ";" => Stmt::BreakStmt,
    "return" <e:Expr?> ";" => Stmt::ReturnStmt(e),
};

StmtList: Vec<Stmt> = (<Stmt>)+;

pub Expr: Expr = {
    <Comma> => Expr::from(<>)
};

Comma: Comma = {
    <Comma> "," <Assignment> => box_arguments!(Comma::Comma, <>),
    <Assignment> => into_expr_type!(Comma, <>)
}

Assignment: Assignment = {
    <Unary> "=" <Assignment> => box_arguments!(Assignment::Assignment, <>),
    <Unary> "+=" <Assignment> => box_arguments!(Assignment::AdditionAssignment, <>),
    <Unary> "-=" <Assignment> => box_arguments!(Assignment::SubtractionAssignment, <>),
    <Unary> "*=" <Assignment> => box_arguments!(Assignment::MultiplicationAssignment, <>),
    <Unary> "/=" <Assignment> => box_arguments!(Assignment::DivisionAssignment, <>),
    <Unary> "%=" <Assignment> => box_arguments!(Assignment::ModuloAssignment, <>),
    <Unary> "&=" <Assignment> => box_arguments!(Assignment::BitwiseAndAssignment, <>),
    <Unary> "|=" <Assignment> => box_arguments!(Assignment::BitwiseOrAssignment, <>),
    <Unary> "^=" <Assignment> => box_arguments!(Assignment::BitwiseXorAssignment, <>),
    <Unary> "<<=" <Assignment> => box_arguments!(Assignment::BitwiseLeftShiftAssignment, <>),
    <Unary> ">>=" <Assignment> => box_arguments!(Assignment::BitwiseRightShiftAssignment, <>),
    <Ternary> => into_expr_type!(Assignment, <>)
};

Unary: Unary = {
    "!" <Unary> => box_arguments!(Unary::UnaryNot, <>),
    "-" <Unary> => box_arguments!(Unary::UnaryMinus, <>),
    "~" <Unary> => box_arguments!(Unary::UnaryBitwiseNot, <>),
    "++" <Unary> => box_arguments!(Unary::PreIncrement, <>),
    "--" <Unary> => box_arguments!(Unary::PreDecrement, <>),
    <Postfix> => into_expr_type!(Unary, <>)
};

Postfix: Postfix = {
    <Postfix> "[" <Expr> "]" => box_arguments!(Postfix::Index, <>),
    <l:Postfix> "." <r:IDENTIFIER> => box_arguments!(Postfix::Dot, l, IDENTIFIER::Identifier(r)),
    <l:Postfix> "::" <r:IDENTIFIER> => box_arguments!(Postfix::NamespaceAccess, l, IDENTIFIER::Identifier(r)),
    <Postfix> "++" => box_arguments!(Postfix::PostIncrement, <>),
    <Postfix> "--" => box_arguments!(Postfix::PostDecrement, <>),
    <i:Postfix> "(" <l:ArgumentList?> ")" => Postfix::Call(Box::new(i), l.unwrap_or(Vec::new())),
    <Primary> => into_expr_type!(Postfix, <>)
};

ArgumentList: Vec<Assignment> = CommaSeparated<Assignment>;

Ternary: Ternary = {
    <Logical> "?" <Expr> ":" <Ternary> => box_arguments!(Ternary::Ternary, <>),
    <Logical> => into_expr_type!(Ternary, <>)
};

Logical: Logical = {
    <Logical> "&&" <Equality> => box_arguments!(Logical::LogicalAnd, <>),
    <Logical> "||" <Equality> => box_arguments!(Logical::LogicalOr, <>),
    <Equality> => into_expr_type!(Logical, <>)
};

Equality: Equality = {
    <Equality> "==" <Bitwise> => box_arguments!(Equality::Equals, <>),
    <Equality> "!=" <Bitwise> => box_arguments!(Equality::NotEquals, <>),
    <Comparison> => into_expr_type!(Equality, <>)
};

Bitwise: Bitwise = {
    <Bitwise> "&" <Comparison> => box_arguments!(Bitwise::BitwiseAnd, <>),
    <Bitwise> "|" <Comparison> => box_arguments!(Bitwise::BitwiseOr, <>),
    <Bitwise> "^" <Comparison> => box_arguments!(Bitwise::BitwiseXor, <>),
    <Comparison> => into_expr_type!(Bitwise, <>)
};

Comparison: Comparison = {
    <Comparison> ">" <BitShift> => box_arguments!(Comparison::GreaterThan, <>),
    <Comparison> ">=" <BitShift> => box_arguments!(Comparison::GreaterThanOrEqualTo, <>),
    <Comparison> "<" <BitShift> => box_arguments!(Comparison::LessThan, <>),
    <Comparison> "<=" <BitShift> => box_arguments!(Comparison::LessThanOrEqualTo, <>),
    <Term> => into_expr_type!(Comparison, <>)
};

BitShift: Shift = {
    <BitShift> ">>" <Term> => box_arguments!(Shift::BitwiseRightShift, <>),
    <BitShift> "<<" <Term> => box_arguments!(Shift::BitwiseLeftShift, <>),
    <Term> => into_expr_type!(Shift, <>)
};

Term: Term = {
    <Term> "+" <Factor> => box_arguments!(Term::Addition, <>),
    <Term> "-" <Factor> => box_arguments!(Term::Subtraction, <>),
    <Factor> => into_expr_type!(Term, <>)
};

Factor: Factor = {
    <Factor> "/" <Unary> => box_arguments!(Factor::Division, <>),
    <Factor> "*" <Unary> => box_arguments!(Factor::Multiplication, <>),
    <Factor> "%" <Unary> => box_arguments!(Factor::Modulo, <>),
    <Unary> => into_expr_type!(Factor, <>)
};

Primary: Primary = {
    NUMBER => Primary::NumberLiteral(<>),
    STRING => Primary::StringLiteral(<>),
    IDENTIFIER => Primary::Identifier(<>),
    "true" => Primary::BooleanLiteral(true),
    "false" => Primary::BooleanLiteral(false),
    "(" <Expr> ")" => box_arguments!(Primary::Parenthesized, <>),
    ! => { errors.push(<>); Primary::Error }
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Star,
        "/" => lexer::Tok::Slash,
        "%" => lexer::Tok::Percent,
        "++" => lexer::Tok::PlusPlus,
        "--" => lexer::Tok::MinusMinus,

        "==" => lexer::Tok::EqEq,
        "!=" => lexer::Tok::NotEq,
        ">" => lexer::Tok::Greater,
        ">=" => lexer::Tok::GreaterEq,
        "<" => lexer::Tok::Less,
        "<=" => lexer::Tok::LessEq,

        "&&" => lexer::Tok::LogicalAnd,
        "||" => lexer::Tok::LogicalOr,
        "!" => lexer::Tok::LogicalNot,

        "&" => lexer::Tok::BitwiseAnd,
        "|" => lexer::Tok::BitwiseOr,
        "^" => lexer::Tok::BitwiseXor,
        "~" => lexer::Tok::BitwiseNot,
        "<<" => lexer::Tok::BitwiseLeftShift,
        ">>" => lexer::Tok::BitwiseRightShift,

        "=" => lexer::Tok::Assign,
        "+=" => lexer::Tok::PlusAssign,
        "-=" => lexer::Tok::MinusAssign,
        "*=" => lexer::Tok::StarAssign,
        "/=" => lexer::Tok::SlashAssign,
        "%=" => lexer::Tok::PercentAssign,
        "&=" => lexer::Tok::BitwiseAndAssign,
        "|=" => lexer::Tok::BitwiseOrAssign,
        "^=" => lexer::Tok::BitwiseXorAssign,
        "<<=" => lexer::Tok::BitwiseLeftShiftAssign,
        ">>=" => lexer::Tok::BitwiseRightShiftAssign,

        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "." => lexer::Tok::Dot,
        "?" => lexer::Tok::QuestionMark,
        ":" => lexer::Tok::Colon,
        "::" => lexer::Tok::ColonColon,

        "(" => lexer::Tok::LeftParen,
        ")" => lexer::Tok::RightParen,
        "{" => lexer::Tok::LeftBrace,
        "}" => lexer::Tok::RightBrace,
        "[" => lexer::Tok::LeftBracket,
        "]" => lexer::Tok::RightBracket,

        STRING => lexer::Tok::StringLiteral(<String>),
        NUMBER => lexer::Tok::NumberLiteral(<String>),
        IDENTIFIER => lexer::Tok::Identifier(<String>),

        "true" => lexer::Tok::True,
        "false" => lexer::Tok::False,
        "if" => lexer::Tok::If,
        "else" => lexer::Tok::Else,
        "while" => lexer::Tok::While,
        "for" => lexer::Tok::For,
        "break" => lexer::Tok::Break,
        "continue" => lexer::Tok::Continue,
        "return" => lexer::Tok::Return,
        "let" => lexer::Tok::Let,
        "fn" => lexer::Tok::Fn,
        "->" => lexer::Tok::SmallArrow,
    }
}