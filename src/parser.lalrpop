use crate::ast::*;
use crate::{lexer, box_arguments, into_expr_type};
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, lexer::Tok, lexer::LexicalError>>);

pub Expr: Expr = {
    <Comma> => Expr::from(<>)
};

pub Comma: Comma = {
    <Comma> "," <Assignment> => box_arguments!(Comma::new_comma, <>),
    <Assignment> => into_expr_type!(Comma, <>)
}

Assignment: Assignment = {
    <Unary> "=" <Assignment> => box_arguments!(Assignment::new_assignment, <>),
    <Unary> "+=" <Assignment> => box_arguments!(Assignment::new_addition_assignment, <>),
    <Unary> "-=" <Assignment> => box_arguments!(Assignment::new_subtraction_assignment, <>),
    <Unary> "*=" <Assignment> => box_arguments!(Assignment::new_multiplication_assignment, <>),
    <Unary> "/=" <Assignment> => box_arguments!(Assignment::new_division_assignment, <>),
    <Unary> "%=" <Assignment> => box_arguments!(Assignment::new_modulo_assignment, <>),
    <Unary> "&=" <Assignment> => box_arguments!(Assignment::new_bitwise_and_assignment, <>),
    <Unary> "|=" <Assignment> => box_arguments!(Assignment::new_bitwise_or_assignment, <>),
    <Unary> "^=" <Assignment> => box_arguments!(Assignment::new_bitwise_xor_assignment, <>),
    <Unary> "<<=" <Assignment> => box_arguments!(Assignment::new_bitwise_left_shift_assignment, <>),
    <Unary> ">>=" <Assignment> => box_arguments!(Assignment::new_bitwise_right_shift_assignment, <>),
    <Ternary> => into_expr_type!(Assignment, <>)
};

Unary: Unary = {
    "!" <Unary> => box_arguments!(Unary::new_unary_not, <>),
    "-" <Unary> => box_arguments!(Unary::new_unary_minus, <>),
    "++" <Unary> => box_arguments!(Unary::new_pre_increment, <>),
    "--" <Unary> => box_arguments!(Unary::new_pre_decrement, <>),
    <Postfix> => into_expr_type!(Unary, <>)
};

Postfix: Postfix = {
    <Postfix> "[" <Expr> "]" => box_arguments!(Postfix::new_index, <>),
    <l:Postfix> "." <r:IDENTIFIER> => box_arguments!(Postfix::new_dot, l, IDENTIFIER::new_identifier(r)),
    <l:Postfix> "::" <r:IDENTIFIER> => box_arguments!(Postfix::new_namespace_access, l, IDENTIFIER::new_identifier(r)),
    <Postfix> "++" => box_arguments!(Postfix::new_post_increment, <>),
    <Postfix> "--" => box_arguments!(Postfix::new_post_decrement, <>),
    <i:Postfix> "(" <l:ArgumentList> ")" => Postfix::new_call(Box::new(i), l),
    <Postfix> "(" ")" => Postfix::new_call(Box::new(<>), vec![]),
    <Primary> => into_expr_type!(Postfix, <>)
};

ArgumentList: Vec<Assignment> = {
    <left:ArgumentList> "," <right:Assignment> => {
        let mut new = left.clone();
        new.push(right);
        new
    },
    <Assignment> => vec![<>]
};

Ternary: Ternary = {
    <Logical> "?" <Expr> ":" <Ternary> => box_arguments!(Ternary::new_ternary, <>),
    <Logical> => into_expr_type!(Ternary, <>)
};

Logical: Logical = {
    <Logical> "&&" <Equality> => box_arguments!(Logical::new_logical_and, <>),
    <Logical> "||" <Equality> => box_arguments!(Logical::new_logical_or, <>),
    <Equality> => into_expr_type!(Logical, <>)
};

Equality: Equality = {
    <Equality> "==" <Bitwise> => box_arguments!(Equality::new_equals, <>),
    <Equality> "!=" <Bitwise> => box_arguments!(Equality::new_not_equals, <>),
    <Comparison> => into_expr_type!(Equality, <>)
};

Bitwise: Bitwise = {
    <Bitwise> "&" <Comparison> => box_arguments!(Bitwise::new_bitwise_and, <>),
    <Bitwise> "|" <Comparison> => box_arguments!(Bitwise::new_bitwise_or, <>),
    <Bitwise> "^" <Comparison> => box_arguments!(Bitwise::new_bitwise_xor, <>),
    <Comparison> => into_expr_type!(Bitwise, <>)
};

Comparison: Comparison = {
    <Comparison> ">" <BitShift> => box_arguments!(Comparison::new_greater_than, <>),
    <Comparison> ">=" <BitShift> => box_arguments!(Comparison::new_greater_than_or_equal_to, <>),
    <Comparison> "<" <BitShift> => box_arguments!(Comparison::new_less_than, <>),
    <Comparison> "<=" <BitShift> => box_arguments!(Comparison::new_less_than_or_equal_to, <>),
    <Term> => into_expr_type!(Comparison, <>)
};

BitShift: Shift = {
    <BitShift> ">>" <Term> => box_arguments!(Shift::new_bitwise_right_shift, <>),
    <BitShift> "<<" <Term> => box_arguments!(Shift::new_bitwise_left_shift, <>),
    <Term> => into_expr_type!(Shift, <>)
};

Term: Term = {
    <Term> "+" <Factor> => box_arguments!(Term::new_addition, <>),
    <Term> "-" <Factor> => box_arguments!(Term::new_subtraction, <>),
    <Factor> => into_expr_type!(Term, <>)
};

Factor: Factor = {
    <Factor> "/" <Unary> => box_arguments!(Factor::new_division, <>),
    <Factor> "*" <Unary> => box_arguments!(Factor::new_multiplication, <>),
    <Factor> "%" <Unary> => box_arguments!(Factor::new_modulo, <>),
    <Unary> => into_expr_type!(Factor, <>)
};

Primary: Primary = {
    NUMBER => Primary::new_number_literal(<>),
    STRING => Primary::new_string_literal(<>),
    IDENTIFIER => Primary::new_identifier(<>),
    "true" => Primary::new_boolean_literal(true),
    "false" => Primary::new_boolean_literal(false),
    "(" <Expr> ")" => box_arguments!(Primary::new_parenthesized, <>),
    ! => { errors.push(<>); Primary::Error }
};

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Star,
        "/" => lexer::Tok::Slash,
        "%" => lexer::Tok::Percent,
        "++" => lexer::Tok::PlusPlus,
        "--" => lexer::Tok::MinusMinus,

        "==" => lexer::Tok::EqEq,
        "!=" => lexer::Tok::NotEq,
        ">" => lexer::Tok::Greater,
        ">=" => lexer::Tok::GreaterEq,
        "<" => lexer::Tok::Less,
        "<=" => lexer::Tok::LessEq,

        "&&" => lexer::Tok::LogicalAnd,
        "||" => lexer::Tok::LogicalOr,
        "!" => lexer::Tok::LogicalNot,

        "&" => lexer::Tok::BitwiseAnd,
        "|" => lexer::Tok::BitwiseOr,
        "^" => lexer::Tok::BitwiseXor,
        "~" => lexer::Tok::BitwiseNot,
        "<<" => lexer::Tok::BitwiseLeftShift,
        ">>" => lexer::Tok::BitwiseRightShift,

        "=" => lexer::Tok::Assign,
        "+=" => lexer::Tok::PlusAssign,
        "-=" => lexer::Tok::MinusAssign,
        "*=" => lexer::Tok::StarAssign,
        "/=" => lexer::Tok::SlashAssign,
        "%=" => lexer::Tok::PercentAssign,
        "&=" => lexer::Tok::BitwiseAndAssign,
        "|=" => lexer::Tok::BitwiseOrAssign,
        "^=" => lexer::Tok::BitwiseXorAssign,
        "<<=" => lexer::Tok::BitwiseLeftShiftAssign,
        ">>=" => lexer::Tok::BitwiseRightShiftAssign,

        ";" => lexer::Tok::Semicolon,
        "," => lexer::Tok::Comma,
        "." => lexer::Tok::Dot,
        "?" => lexer::Tok::QuestionMark,
        ":" => lexer::Tok::Colon,
        "::" => lexer::Tok::ColonColon,

        "(" => lexer::Tok::LeftParen,
        ")" => lexer::Tok::RightParen,
        "{" => lexer::Tok::LeftBrace,
        "}" => lexer::Tok::RightBrace,
        "[" => lexer::Tok::LeftBracket,
        "]" => lexer::Tok::RightBracket,

        STRING => lexer::Tok::StringLiteral(<String>),
        NUMBER => lexer::Tok::NumberLiteral(<String>),
        IDENTIFIER => lexer::Tok::Identifier(<String>),

        "true" => lexer::Tok::True,
        "false" => lexer::Tok::False,
    }
}